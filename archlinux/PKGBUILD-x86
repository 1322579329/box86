# Maintainer: Jai-JAP <parjailu@gmail.com>, SpacingBat3 <git@spacingbat3.anonaddy.com>
# Author: Sebastien Chevalier <ptitseb@box86.org>
pkgname=box86-x86-git
pkgver=3494.bedacef
pkgrel=1
pkgdesc="Linux Userspace x86 Emulator with a twist, targeted at ARM Linux devices."
arch=('i386')
url="https://github.com/ptitSeb/box86"
license=('MIT')
makedepends=('git' 'cmake' 'make')
provides=("${pkgname%-git}" "${pkgname%-x86-git}-git" "${pkgname%-x86-git}")
conflicts=("${pkgname%-git}" "${pkgname%-x86-git}-git" "${pkgname%-x86-git}")
source=('git+https://github.com/ptitSeb/box86')
md5sums=('SKIP')

pkgver() {
	cd "$srcdir/${pkgname%-x86-git}"
	printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
	cd "${srcdir}/${pkgname%-x86-git}"
	if [[ ! -d ./build ]]; then
		mkdir build && cd build
		cmake .. -DLD80BITS=1 -DNOALIGN=1 -DCMAKE_BUILD_TYPE=RelWithDebInfo
	fi
}

build() {
	cd "$srcdir/${pkgname%-x86-git}/build"
	make -j$(nproc)
}

package() {
	cd "$srcdir/${pkgname%-x86-git}/build"
	make DESTDIR="${pkgdir}/" install
	# /usr/local/bin isn't in PATH by the default,
	# we should move it to /usr/bin
	cd ${pkgdir}
	mv usr/local/bin/ usr/bin/
	# cleanup when dir is empty
	rmdir usr/local || exit 0
}
